%import common.DIGIT
%import common.WS
%import common.LETTER
%import common.CPP_COMMENT
%import common.C_COMMENT

%ignore WS

// comments are ignored for now but should stay
%ignore CPP_COMMENT
%ignore C_COMMENT

IDENTIFIER: LETTER (LETTER | DIGIT | "_")*
FQN: IDENTIFIER ("." IDENTIFIER)*

start: package_decl ";" _package_body

package_decl: "package" FQN

_package_body: (component | bus | intf)*


component: "comp"  IDENTIFIER "{" _component_block* "}"

_component_block: _in_block
                | _out_block
                | _data_block
                | _run_block

_in_block: "in" "{" input* "}"
_out_block: "out" "{" output* "}"
_data_block: "data" "{" datum* "}"
_run_block: "run" "{" run* "}"

input: IDENTIFIER ":" TYPENAME ";"?
output: IDENTIFIER ":" TYPENAME ";"?
datum: IDENTIFIER ":" TYPENAME ";"?

run: IDENTIFIER "[" run_trigger (";" run_output)? "]" ";"?
run_trigger: "on" FQN
run_output: "to" FQN

bus: "bus" IDENTIFIER ":" TYPENAME ";"

intf: "intf" IDENTIFIER "{" _intf_member "}"
_intf_member: method_decl

method_decl: IDENTIFIER "(" arg ("," arg)* ")" ":" TYPENAME
arg: IDENTIFIER ":" TYPENAME
TYPENAME: IDENTIFIER
